---
title: "Using Rive with Unity 2D"
description: ''
---

Rive renders to a **Render Texture** in Unity, making it compatible with Unity features that support Render Textures. This approach provides flexibility but requires specific considerations when working with Unity's 2D rendering pipeline.

## Working with Sprite Renderers

While Rive works well with Unity overall, **Sprite Renderers** don't [natively handle Render Textures optimally](https://discussions.unity.com/t/how-to-apply-a-render-texture-from-a-camera-to-a-sprite-for-an-object-with-sprite-renderer/257199). There are two ways to work around this:

1. **Using Rive Texture Renderer with Quads (Recommended)**

   * Render Rive graphics to a Quad using the **Rive Texture Renderer** component

   * Does not require a shader workaround

   * Works the same in 2D and 3D

2. **Custom Shader Solution for Sprite Renderers**

   * Useful when you need to maintain precise sorting order with other sprites

   * Allows Rive content to work within the existing sprite rendering pipeline

   * Requires a custom shader implementation

<Warning>
  When using the custom shader approach with SpriteRenderers, pointer input is not automatically supported. This is because SpriteRenderers and SpriteColliders do not provide `textureCoords` like Quads and MeshColliders do.&#x20;

  If you need pointer input support, consider using the recommended Quad approach with RiveTextureRenderer instead.
</Warning>

If you need to maintain sort order when mixing Rive graphics with **Sprite Renderers**, you can reference the following custom shader as a starting point:

```
Shader "Custom/SpriteTextureOverride"
{
    Properties
    {
        // SpriteRenderer will automatically assign a texture here at runtime
        // We declare it but won't use it
        _MainTex ("Default Sprite", 2D) = "white" {}
        
        // This is our actual texture that we want to display
        // Could be a RenderTexture or any other texture
        _OverrideTexture ("Override Texture", 2D) = "white" {}
        
        // Required for sprite renderer sorting
        [HideInInspector] _Cutoff ("Cutoff", Float) = 0.5
        [HideInInspector] _Color ("Tint", Color) = (1,1,1,1)
    }
    
    SubShader
    {
        Tags
        { 
            "Queue"="Transparent" 
            "RenderType"="Transparent" 
            "IgnoreProjector"="True"
            "PreviewType"="Plane"
        }
        
        Cull Off
        Lighting Off
        ZWrite Off
        Blend One OneMinusSrcAlpha

        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #include "UnityCG.cginc"
            
            struct appdata
            {
                float4 vertex : POSITION;
                float2 uv : TEXCOORD0;
                fixed4 color : COLOR;
            };
            
            struct v2f
            {
                float2 uv : TEXCOORD0;
                float4 vertex : SV_POSITION;
                fixed4 color : COLOR;
            }; 
            
            sampler2D _MainTex;
            float4 _MainTex_ST;
            sampler2D _OverrideTexture;
            float4 _OverrideTexture_ST;
            fixed4 _Color;
            
            v2f vert (appdata v)
            {
                v2f o;
                o.vertex = UnityObjectToClipPos(v.vertex);
                o.uv = TRANSFORM_TEX(v.uv, _MainTex);
                o.color = v.color * _Color;
                return o;
            }
            
            fixed4 frag (v2f i) : SV_Target
            {
                fixed4 color = tex2D(_OverrideTexture, i.uv);
                color.rgb *= color.a;
                return color * i.color;
            }
            ENDCG
        }
    }
}
```

To implement this:

1. Create a new shader file in your project and paste the above code

2. Create a new material using this shader

3. Assign the material to your **Sprite Renderer**

4. Use **Rive Texture Renderer** to set the `OverrideTexture` property with your Render Texture

![Setting the OverrideTexture property with a Rive Texture Renderer](/images/unity/spriterenderer-override-texture-property.jpg)